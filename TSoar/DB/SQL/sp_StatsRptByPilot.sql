SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
IF (OBJECT_ID('sp_StatsRptByPilot') IS NOT NULL) DROP PROCEDURE sp_StatsRptByPilot
GO
-- Generate report on piloting statistics by Aviator, Year, Glider, and Aviator Role
CREATE PROCEDURE sp_StatsRptByPilot
AS
BEGIN
	SELECT (IIF(ISNULL(MEMBERFROMTO.iPerson,0)=0, N'Temporary Member', PEOPLE.sDisplayName)) AS Aviator,
		YEAR(OPERATIONS.DBegin) AS Year, EQUIPMENT.sShortEquipName AS Glider, AVIATORROLES.sAviatorRole AS [Role],
		ROUND(SUM(DATEDIFF(mi, OPERATIONS.DBegin, OPERATIONS.DEnd)) / 60.0, 2) AS [Flight Hours],
		MAX(' ') AS SubGlider,	MAX(' ') AS SubYear, MAX(' ') AS SubAviator, MAX(' ') AS Total
		FROM AVIATORS
			INNER JOIN AVIATORROLES ON AVIATORS.iAviatorRole = AVIATORROLES.ID
			INNER JOIN OPDETAILS ON AVIATORS.iOpDetail = OPDETAILS.ID
			INNER JOIN EQUIPMENT ON OPDETAILS.iEquip = EQUIPMENT.ID
			INNER JOIN OPERATIONS ON OPDETAILS.iOperation = OPERATIONS.ID
			INNER JOIN PEOPLE ON AVIATORS.iPerson = PEOPLE.ID
			LEFT JOIN MEMBERFROMTO ON PEOPLE.ID = MEMBERFROMTO.iPerson
			INNER JOIN EQUIPMENTROLES ON OPDETAILS.iEquipmentRole = EQUIPMENTROLES.ID
		WHERE (EQUIPMENTROLES.sEquipmentRole = N'Glider')
			AND (OPERATIONS.DBegin >= CONVERT(DATETIME, '2017-01-01 00:00:00', 102))
			AND (AVIATORROLES.sAviatorRole <> N'ChargeTo')
		GROUP BY (IIF(ISNULL(MEMBERFROMTO.iPerson,0)=0, N'Temporary Member', PEOPLE.sDisplayName)),
			YEAR(OPERATIONS.DBegin),
			EQUIPMENT.sShortEquipName, AVIATORROLES.sAviatorRole WITH ROLLUP
END